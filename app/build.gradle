import java.nio.file.Files
import java.nio.file.Paths
import java.util.regex.Pattern
import java.util.zip.ZipEntry
import java.util.zip.ZipInputStream
import java.util.zip.ZipOutputStream

apply plugin: 'apk-conventions'
apply from: "$rootDir/configs/foss-pro-flavors.gradle"

android {
    namespace 'com.mirfatif.permissionmanagerx'

    defaultConfig {
        applicationId namespace

        versionCode libs.versions.app.code.get().toInteger()
        versionName libs.versions.app.name.get()

        // Flavor-independent and BuildType-independent BuildConfig.APPLICATION_ID
        buildConfigField 'String', 'APP_ID', '"' + namespace + '"'

        // Daemon dex file to be saved in assets directory.
        buildConfigField 'String', 'DAEMON_DEX', '"' + daemonDex + '"'
    }

    flavorDimensions.add(0, 'type')
    productFlavors {
        self {
            dimension 'type'
        }
        ps {
            dimension 'type'
            applicationIdSuffix '.ps'
            versionNameSuffix '-ps'
        }
        fdroid {
            dimension 'type'
            versionNameSuffix '-fd'
        }
        amaz {
            dimension 'type'
            versionNameSuffix '-amaz'
        }
        pro {
            applicationIdSuffix '.pro'
            versionNameSuffix '-pro'
        }
    }

    variantFilter { variant ->
        List names = variant.flavors*.name
        if (noPro) {
            if (!names.contains('fdroid') || !names.contains('foss')) {
                variant.setIgnore(true)
            }
        } else if (names.contains('ps') && names.contains('foss')) {
            variant.setIgnore(true)
        } else if ((names.contains('fdroid') || names.contains('amaz')) && names.contains('pro')) {
            variant.setIgnore(true)
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'
        }
    }

    applicationVariants.configureEach { variant ->
        def mf = variant.mergedFlavor
        if (variant.flavorName == 'psPro' && variant.buildType.name == 'release') {
            // No .ps.pro suffix for Play Store Pro release
            mf.setApplicationId(namespace)
        }

        String lfp = variant.applicationId + '.LogFileProvider'
        mf.addManifestPlaceholders(LOG_FILE_PROVIDER: lfp)
        variant.buildConfigField 'String', 'LOG_FILE_PROVIDER', '"' + lfp + '"'

        // print(variant.name + ' ' + variant.applicationId + ' ')
        // print(mf.manifestPlaceholders)
        // println()
    }

    sourceSets.configureEach { sc ->
        // These combinations are not created by default (required for daemon dex).
        def dir
        if (sc.name.matches('(self|ps)ProDebug')) {
            dir = 'proDebug'
        } else if (sc.name.matches('(self|ps)ProRelease')) {
            dir = 'proRelease'
        } else if (sc.name.matches('(self|fdroid|amaz)FossDebug')) {
            dir = 'fossDebug'
        } else if (sc.name.matches('(self|fdroid|amaz)FossRelease')) {
            dir = 'fossRelease'
        } else {
            return
        }
        sc.assets.srcDirs += ["$projectDir/src/$dir/assets"]
    }

    packagingOptions {
        jniLibs {
            // Due to android:extractNativeLibs set to 'true' in AndroidManifest.
            useLegacyPackaging true
        }

        // Exclude large files.
        resources.excludes.add('org/bouncycastle/pqc/crypto/picnic/lowmcL1.bin.properties')
        resources.excludes.add('org/bouncycastle/pqc/crypto/picnic/lowmcL3.bin.properties')
        resources.excludes.add('org/bouncycastle/pqc/crypto/picnic/lowmcL5.bin.properties')
    }
}

configurations {
    configureEach {
        // To avoid duplicate class version conflict.
        exclude group: 'androidx.lifecycle', module: 'lifecycle-viewmodel-ktx'
    }
}

dependencies {
    implementation project(path: ':priv_library')

    implementation libs.libadb.android
    // To generate X509Certificate
    implementation libs.sun.security.android
    // For conscrypt
    implementation libs.hiddenapibypass

    implementation libs.androidx.appcompat
    implementation libs.androidx.recyclerview
    implementation libs.androidx.swiperefreshlayout
    implementation libs.androidx.preference
    implementation libs.androidx.browser
    implementation libs.androidx.room.runtime
    implementation libs.androidx.security.crypto
    implementation libs.androidx.webkit
    annotationProcessor libs.androidx.room.compiler

    // For SnackBar and NavigationView. Also includes CoordinatorLayout
    implementation libs.material

    // To handle hyperlink onClick in TextView
    implementation libs.better.link.movement.method

    // To use Iterables for splitting Collections
    implementation(libs.guava)

    // Let's behave responsibly
    debugImplementation libs.leakcanary.android
}

lsparanoid {
    variantFilter {
        !it.name.contains('Foss') && it.buildType != 'debug'
    }
}

def createJarFromApk(String apkFile, String jarFile) {
    try (ZipInputStream apk = new ZipInputStream(new FileInputStream(apkFile))) {
        try (ZipOutputStream jar = new ZipOutputStream(new FileOutputStream(jarFile))) {
            ZipEntry entry
            Pattern clsDex = Pattern.compile("classes[0-9]*.dex")
            int len
            byte[] buf = new byte[1024]

            while ((entry = apk.nextEntry) != null) {
                if (entry.name.matches(clsDex)) {
                    jar.putNextEntry(new ZipEntry(entry.name))
                    while ((len = apk.read(buf)) > 0) {
                        jar.write(buf, 0, len)
                    }
                    jar.closeEntry()
                }
                apk.closeEntry()
            }
        }
    }

    if (!noPro) {
        println("Jar created: $jarFile")
    }
}

def copyFileIfChanged(File src, File dst) {
    assert src.file
    assert !dst.exists() || dst.file

    if (dst.exists()) {
        byte[] s = Files.readAllBytes(Paths.get(src.absolutePath))
        byte[] d = Files.readAllBytes(Paths.get(dst.absolutePath))
        if (Arrays.equals(s, d)) {
            println('UP-TO-DATE: ' + dst.absolutePath)
            return
        }

        delete dst
    }

    copy {
        from src
        into dst.parentFile
        rename { fileName -> dst }
    }

    if (dst.exists()) {
        if (!noPro) {
            println('Creating: ' + dst.absolutePath)
        }
    } else {
        throw new GradleException('Failed to create: ' + dst.absolutePath)
    }
}

def createTasksForDaemonBuild = () -> {
    boolean signed = project.hasProperty('android.injected.signing.store.file')

    for (boolean foss : new boolean[]{true, false}) {
        if (noPro && !foss) {
            continue
        }

        for (boolean debug : new boolean[]{true, false}) {
            String task = foss ? 'Foss' : 'Pro'
            task += debug ? 'Debug' : 'Release'

            String dir = foss ? 'foss' : 'pro'
            dir += debug ? 'Debug' : 'Release'

            String dexTaskDep = 'assemble' + task

            String apk = 'priv_daemon/build/outputs/apk/' + (foss ? 'foss' : 'pro') + '/'
            apk += (debug ? 'debug' : 'release') + '/priv_daemon-' + (foss ? 'foss' : 'pro')
            apk += (debug ? '-debug' : ('-release' + (signed ? '' : '-unsigned'))) + '.apk'

            String jar = apk.replace('.apk', '.jar')

            File srcFile = new File(rootDir, jar)
            File dstFile = new File(rootDir, 'app/src/' + dir + '/assets/' + daemonDex)

            Task t = tasks.register('buildDaemon' + task) {
                dependsOn(':priv_daemon:' + dexTaskDep)
                mustRunAfter(':priv_daemon:' + dexTaskDep)

                ext.srcFile = srcFile
                ext.dstFile = dstFile

                doLast {
                    createJarFromApk(apk, jar)
                }
            }.get()

            if (foss) {
                t.doLast {
                    copyFileIfChanged(srcFile, dstFile)
                }
            }
        }
    }
}

tasks.register('buildNativeFoss', Exec) {
    environment 'ANDROID_NDK', ndkDir
    workingDir nativeDir
    commandLine './build_native.sh'

    doLast {
        if (executionResult.get().exitValue != 0) {
            throw new GradleException('buildNativeFoss failed')
        }
    }
}

def setExtSrcDependencies(String type, String version) {
    for (boolean debug : new boolean[]{true, false}) {
        String build = debug ? 'Debug' : 'Release'

        TaskProvider task = tasks.named('merge' + type + version + build + 'Assets')

        task.configure {
            dependsOn('buildDaemon' + version + build)
            mustRunAfter('buildDaemon' + version + build)
        }

        task = tasks.named('merge' + type + version + build + 'JniLibFolders')

        task.configure {
            if (version == 'Foss') {
                dependsOn('buildNativeFoss')
                mustRunAfter('buildNativeFoss')
            } else {
                dependsOn('buildNative' + type + version + build)
                mustRunAfter('buildNative' + type + version + build)
            }
        }
    }
}

createTasksForDaemonBuild()

if (!noPro) {
    apply from: '../configs/pro-app.gradle'
}

afterEvaluate {
    setExtSrcDependencies('Fdroid', 'Foss')

    if (!noPro) {
        setExtSrcDependencies('Self', 'Foss')
        setExtSrcDependencies('Amaz', 'Foss')
        setExtSrcDependencies('Self', 'Pro')
        setExtSrcDependencies('Ps', 'Pro')
    }
}

clean.doLast {
    // Delete daemon dex
    for (String variant in new String[]{'fossRelease', 'fossDebug', 'proRelease', 'proDebug'}) {
        Paths.get(projectDir.absolutePath, 'src/' + variant + '/assets').deleteDir()
    }
    // Delete native libraries
    for (String variant in new String[]{'main', 'foss', 'selfProRelease',
            'psProRelease', 'selfProDebug', 'psProDebug'}) {
        Paths.get(projectDir.absolutePath, 'src/' + variant + '/jniLibs').deleteDir()
    }
}

preBuild.doFirst {
    // Re-merge the assets and jniLibs source sets.
    for (String dir in new String[]{'assets', 'merged_jni_libs'}) {
        Paths.get(projectDir.absolutePath, 'build/intermediates/' + dir).deleteDir()
    }
}

apply plugin: 'com.android.application'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.mirfatif.permissionmanagerx"
        minSdkVersion 24
        targetSdkVersion 30
        versionCode 103
        versionName "v1.03-beta2"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dependenciesInfo {
        includeInApk false
    }
}

dependencies {
    implementation project(path: ':AdbLib')
    implementation project(path: ':priv_library')

    implementation 'androidx.appcompat:appcompat:1.3.0-alpha02'
    implementation 'androidx.recyclerview:recyclerview:1.2.0-beta01'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.2.0-alpha01'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.browser:browser:1.3.0'
    implementation 'androidx.room:room-runtime:2.3.0-alpha04'
    implementation "androidx.security:security-crypto:1.1.0-alpha03"
    annotationProcessor 'androidx.room:room-compiler:2.3.0-alpha04'

    // For SnackBar and NavigationView. Also includes CoordinatorLayout
    implementation 'com.google.android.material:material:1.3.0-beta01'

    // To handle hyperlink onClick in TextView
    implementation 'me.saket:better-link-movement-method:2.2.0'

    // Let's behave responsibly
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.5'
}

task buildDaemon() {
    def isDebug = false
    for (String taskName : gradle.startParameter.taskNames) {
        if (taskName.contains("Debug")) {
            isDebug = true
            break
        }
    }
    def src = "priv_daemon/build/outputs/apk/release/priv_daemon-release.apk"
    if (isDebug) {
        src = "priv_daemon/build/outputs/apk/debug/priv_daemon-debug.apk"
        dependsOn(':priv_daemon:assembleDebug')
        mustRunAfter(':priv_daemon:assembleDebug')
    } else {
        dependsOn(':priv_daemon:assembleRelease')
        mustRunAfter(':priv_daemon:assembleRelease')
    }

    doLast {
        def dexFile = 'daemon.dex'
        def dstDir = new File(rootDir, "app/src/main/assets/")
        def dstFile = new File(dstDir, dexFile)
        delete dstFile
        copy {
            from zipTree(new File(rootDir, src)).matching { include 'classes.dex' }.singleFile
            into dstDir
            rename { fileName -> dexFile }
        }
        if (!dstFile.exists()) {
            throw new GradleException("buildDaemon failed!\n" + dstFile.getPath() + " does not exist")
        }
    }
}
preBuild.dependsOn(buildDaemon)

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def ndkDir = properties.getProperty('ndk.dir')

task buildSetPriv(type: Exec) {
    environment 'ANDROID_NDK', ndkDir
    workingDir "$rootDir"
    commandLine './build_set_priv.sh'
    doLast {
        if (execResult.getExitValue() != 0) {
            println "buildSetPriv failed!"
        }
    }
}
preBuild.dependsOn(buildSetPriv)

apply plugin: 'com.github.sherter.google-java-format'
preBuild.dependsOn(verifyGoogleJavaFormat)
